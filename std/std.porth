
macro / divmod drop end
macro % divmod swap drop end

macro div divmod drop end
macro mod divmod swap drop end

macro print swap drop stdout end

macro !32 store32 end
macro @32 load32 end

macro !8 store8 end
macro @8 load8 end


memory dump_buffer
    12
end
macro iprint
    // isNeg (valToPus^0xFFFFFFFF+1) 
    dup 31 shr 1 = dup if 
        swap 4294967295 bxor
        1 + 
    else
        swap
    end
    
    11 swap

    dump_buffer 12 + '\n' !8
    while dup 0 != do
        10 divmod '0' +
        2over dump_buffer  +
        swap !8
        swap 1 -
        swap
    end drop

    swap if 
        dup dump_buffer + '-' !8
    else
        1 +
    end
    dump_buffer + stdout
end

memory memcpy_size 4 end
memory memcpy_src 4 end
memory memcpy_dst 4 end

//param: size dst src
macro memcpy
    memcpy_dst  swap !32
    memcpy_src  swap !32
    memcpy_size swap !32

    memcpy_dst @32

    0 while dup memcpy_size @32 < do
        dup memcpy_src  @32 + @8
        over memcpy_dst @32 + swap !8
        1 +
    end drop
end


// param: fileName
macro fopen
    swap drop
    0 swap
    sysval "FILE_ATTRIBUTE_NORMAL" swap
    sysval "OPEN_ALWAYS" swap 
    0 swap
    sysval "FILE_SHARE_READ OR FILE_SHARE_WRITE" swap
    sysval "GENERIC_READ OR GENERIC_WRITE" swap
    syscall "CreateFile"
end

memory fwrite_fh 4 end
memory fwrite_str_to_write 4 end
memory fwrite_strlen 4 end

// param: strlen, strToWrite, fileHandle
macro fwrite
    fwrite_fh swap !32
    fwrite_str_to_write swap !32
    fwrite_strlen swap !32
    0
    0
    fwrite_strlen @32
    fwrite_str_to_write @32
    fwrite_fh @32
    syscall "WriteFile" drop
end

memory fread_fh 4 end
memory fread_buffer 4 end
memory fread_amt_to_read 4 end

// param: amtToRead, buffer, fileHandle
macro fread
    fread_fh swap !32
    fread_buffer swap !32
    fread_amt_to_read swap !32
    0
    0
    fread_amt_to_read @32
    fread_buffer @32
    fread_fh @32
    syscall "ReadFile" drop
end

memory truncate_fh 4 end

// param: fileHandle
macro truncate
    truncate_fh swap !32 //fh
    sysval "FILE_BEGIN" 0 0 truncate_fh @32 syscall "SetFilePointer" drop
    truncate_fh @32 syscall "SetEndOfFile" drop
end

// param: fileHandle
macro fclose
    syscall "CloseHandle" drop
end

// 

memory set_fp_fh 4 end
memory set_fp_move 4 end
memory set_fp_amt_to_move 4 end
macro set_fp
    set_fp_fh swap !32 
    set_fp_move swap !32 
    set_fp_amt_to_move swap !32 
    set_fp_move @32
    0
    set_fp_amt_to_move @32
    set_fp_fh @32
    syscall "SetFilePointer"
end

macro getArgs
syscall "GetCommandLine"
end


// depreciated macros
macro .32 store32 end
macro ,32 load32 end

macro . store8 end
macro , load8 end
